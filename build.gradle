import java.util.stream.Collectors

plugins {
    id "architectury-plugin" version "2.0.66"
    id "forgified-fabric-loom" version "0.6.59" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "forgified-fabric-loom"

    def fcMixinConfigs = [
            "predicates",
            "fastmap",
            "mrl",
            "dedupmultipart",
            "blockstatecache",
            "dedupbakedquad",
        ].stream()
        .map({s -> rootProject.archives_base_name+"."+s+".mixin.json"})
        .collect(Collectors.toList())
    loom {
        silentMojangMappingsLicense()
        mixinConfigs += fcMixinConfigs
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        if (JavaVersion.current().isJava9Compatible()) {
            options.release = 8
        }
    }

    java {
        withSourcesJar()
    }
}
